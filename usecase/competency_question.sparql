prefix schema: <http://schema.org/>
prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
prefix rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
prefix cbam: <https://purl.org/cbam/cbamreport/>
prefix cn: <https://purl.org/cbam/CN/>
prefix foaf: <http://xmlns.com/foaf/0.1/>

select
   ?cnCode
   ?installationName
   (str(?directEmissions_dt) as ?directEmissions)
   (str(?indirectEmissions_dt) as ?indirectEmissions)
   (str(?totalEmissions_dt) as ?totalEmissions)
   ?totalEmissionsUnit
   (str(?carbonCost_dt) as ?carbonCost)
where {
   ?product  rdf:type cbam:Good ;
      cbam:hasCNCode ?cn ;
      cbam:isManufacturedAt ?installation ;
      cbam:hasGreenHouseGasEmissions ?emissions ;
      cbam:hasCarbonCharge ?carbonPrice .

   ?cn cn:cn_code ?cnCode .
   ?installation foaf:name ?installationName .

   ?emissions rdf:type cbam:GreenHouseGasEmissions ;
      cbam:total_Direct_Indirect_Embedded_Emission ?totalEmissions_dt ;
      cbam:DirectEmbeddedEmissions ?directEmissions_dt ;
      cbam:IndirectEmbeddedEmissions ?indirectEmissions_dt ;
      cbam:emissionsUnit ?totalEmissionsUnit .

   ?carbonPrice cbam:resultEffectiveCPDue ?carbonCost_dt .
} order by ?cnCode
